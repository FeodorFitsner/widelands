Import('*')

#TODO: find a way to remove the .orig files astyle leaves around. Probably in
#      scons-tools/astyle.py

SRC=Glob('*.cc')

srcenv=env.Copy()

srcenv.Append(CPPPATH=[
		'#/src',
		'#/src/ui/ui_basic',
		'#/src/ui/ui_fs_menus',
		'#/src/editor',
		'#/src/editor/tools',
		'#/src/editor/ui_menus',
		])

Export('srcenv', 'Glob')
srcenv.Append(LIBS='uibasic', LIBPATH='ui/ui_basic')
srcenv.Append(LIBS='uifsmenus', LIBPATH='ui/ui_fs_menus')
srcenv.Append(LIBS='editor', LIBPATH='editor')
srcenv.Append(LIBS='editoruimenus', LIBPATH='editor/ui_menus')
srcenv.Append(LIBS='editortools', LIBPATH='editor/tools')

SConscript('editor/SConscript')
SConscript('ui/ui_basic/SConscript')
SConscript('ui/ui_fs_menus/SConscript')

INDENTLIST=[
	'disk_filesystem.h',
	'disk_filesystem.cc',
	'filesystem.h',
	'filesystem_exceptions.h',
	'fxset.h',
	'fxset.cc',
	'geometry.h',
	'helper.h',
	'helper.cc',
	'journal.h',
	'journal.cc',
	'journal_exceptions.h',
	'journal_exceptions.cc',
	'layered_filesystem.h',
	'layered_filesystem.cc',
	'main.cc',
	'songset.h',
	'songset.cc',
	'sound_handler.h',
	'sound_handler.cc',
	'wlapplication.h',
	'wlapplication.cc',
	'zip_exceptions.h',
	]
indent=srcenv.astyle(source=INDENTLIST)
env.Alias("indent", indent)

binary=srcenv.Program(target='widelands', source=SRC)
movebinary=srcenv.Command('#/widelands', binary, Move('widelands', BUILDDIR+'/widelands'))

Return('movebinary')
